# Waterproof Tutorial

## 1. We conclude that

### Example:

Lemma example_reflexivity :
  0 = 0.
Proof.
We conclude that (0 = 0).
Qed.

Lemma exercise_reflexivity :
  3 = 3.
Proof.
We conclude that (3 = 3).
Qed.

## 2. We need to show that
Sometimes it is useful to remind the reader or yourself of what you need to show.

### Example

Lemma example_we_need_to_show_that :
  2 = 2.
Proof.
We need to show that (2 = 2).
We conclude that (2 = 2).
Qed.

Lemma exercise_we_need_to_show_that :
  7 = 7.
Proof.
We need to show that (7 = 7).
We conclude that (7 = 7).
Qed.

## 3. Show for-all statements: take arbitrary values

### Example:

Lemma example_take :
  ∀ x ∈ ℝ,
    x = x.
Proof.
Take x ∈ ℝ.
We conclude that (x = x).
Qed.

Lemma exercise_take :
  ∀ x ∈ ℝ,
    x + 3 = 3 + x.
Proof.
Take x ∈ ℝ.
We conclude that (x + 3 = 3 + x).
Qed.

## 4. Show there-exists statements: choose values

### Example

Lemma example_choose :
  ∃ y ∈ ℝ,
    y < 3.
Proof.
Choose y := (2).
* Indeed, (y ∈ ℝ).
* We conclude that (y < 3).
Qed.

Lemma exercise_choose :
  ∃ z > 10,
    z < 14.
Proof.
Choose z := (12).
* Indeed, (z > 10).
* We conclude that (z < 14).
Qed.

## 5. Combine for-all and there-exists statements

### Example

Lemma example_combine_quantifiers :
  ∀ a ∈ ℝ,
    ∀ b > 5,
      ∃ c ∈ ℝ,
        c > b - a.
Proof.
Take a ∈ (ℝ).
Take b > 5.
Choose c := (b - a + 1).
* Indeed, (c ∈ ℝ).
* We conclude that (c > b - a).
Qed.

Lemma exercise_combine_quantifiers :
  ∀ x > 3,
    ∀ y ≥ 4,
      ∃ z ∈ ℝ,
        x < z - y.
Proof.
Take x > 3.
Take y ≥ 4.
Choose z := (x + y + 1).
* Indeed, (z ∈ ℝ).
* We conclude that (x < z - y).
Qed.

## 6.  Make an assumption

### Example

Lemma example_assumptions :
  ∀ a ∈ ℝ,
    a < 0 ⇒ - a > 0.
Proof.
Take a ∈ (ℝ).
Assume that (a < 0).
We conclude that (- a > 0).
Qed.

### Another example with explicit labels

Lemma example_assumptions_2 :
  ∀ a ∈ ℝ,
    a < 0 ⇒ - a > 0.
Proof.
Take a ∈ (ℝ).
Assume that (a < 0) (i). (* The label here is optional *)
By (i) we conclude that (- a > 0).
Qed.

Lemma exercise_assumptions :
  ∀ a ≥ 2,
    ∀ b ∈ ℝ,
      a > 0 ⇒ (b > 0 ⇒ a + b > - 1).
Proof.
Take a ≥ 2.
Take b ∈ ℝ.
Assume that (a > 0).
Assume that (b > 0).
We conclude that (a + b > - 1).
Qed.

## 7. Chains of (in)equalities
Using multiple inequalities requires using special syntax

### Example

Variable f : ℝ → ℝ.
Parameter f_increasing : ∀ x ∈ ℝ, ∀ y ∈ ℝ, x ≤ y ⇒ f(x) ≤ f(y).

Lemma example_inequalities:
  2 < f(0) ⇒ 2 < f(1).
Proof.
Assume that (2 < f(0)).
By (f_increasing) we conclude that (& 2 < f(0) ≤ f(1)).
Qed.

Lemma exercise_inequalities:
  f(3) < 5 ⇒ f(-1) < 5.
Proof.
Assume that (f(3) < 5).
By (f_increasing) we conclude that (& f(-1) ≤ f(3) < 5).
Qed.

## 8. Backwards reasoning in smaller steps

### Example

Lemma example_backwards :
  3 < f(0) ⇒ 2 < f(5).
Proof.
Assume that (3 < f(0)).
It suffices to show that (f(0) ≤ f(5)).
By (f_increasing) we conclude that (f(0) ≤ f(5)).
Qed.

Lemma exercise_backwards :
  f(5) < 4 ⇒ f(-2) < 5.
Proof.
Assume that (f(5) < 4).
It suffices to show that (f(-2) ≤ f(5)).
By (f_increasing) we conclude that (f(-2) ≤ f(5)).
Qed.

## 9. Forwards reasoning in smaller steps

### Example

Lemma example_forwards :
  7 < f(-1) ⇒ 2 < f(6).
Proof.
Assume that (7 < f(-1)).
By (f_increasing) it holds that (f(-1) ≤ f(6)).
We conclude that (2 < f(6)).
Qed.

Lemma exercise_forwards :
  f(7) < 8 ⇒ f(3) ≤ 10.
Proof.
Assume that (f(7) < 8).
By (f_increasing) it holds that (f(7) ≥ f(3)).
We conclude that (f(3) ≤ 10).
Qed.

## 10. Use a *for-all* statement

### Example

Lemma example_use_for_all :
  ∀ x ∈ ℝ,
    (∀ ε > 0, x < ε) ⇒
       x + 1/2 < 1.
Proof.
Take x ∈ ℝ.
Assume that (∀ ε > 0, x < ε) (i).
Use ε := (1/2) in (i).
* Indeed, (1 / 2 > 0).
* It holds that  (x < 1 / 2).
  We conclude that (x + 1/2 < 1).
Qed.

Lemma exercise_use_for_all:
  ∀ x ∈ ℝ,
    (∀ ε > 0, x < ε) ⇒
       10 * x < 1.
Proof.
Take x ∈ (ℝ).
Assume that (∀ ε > 0, x < ε) (i).
Use ε := (1 / 10) in (i).
* Indeed, (1 / 10 > 0).
* It holds that (x < 1/10).
We conclude that (10 * x < 1).
Qed.

## 11. Use a *there-exists* statement

### Example

Lemma example_use_there_exists :
  ∀ x ∈ ℝ,
    (∃ y > 10, y < x) ⇒
      10 < x.
Proof.
Take x ∈ ℝ.
Assume that (∃ y > 10, y < x) (i).
Obtain such a y.
We conclude that (& 10 < y < x).
Qed.

### Another example

Lemma example_use_there_exists_2 :
  ∀ x ∈ ℝ,
    (∃ y > 14, y < x) ⇒
      12 < x.
Proof.
Take x ∈ ℝ.
Assume that (∃ y > 14, y < x) (i).
Obtain y according to (i).
We conclude that (& 12 < y < x).
Qed.

Lemma exercise_use_there_exists :
  ∀ z ∈ ℝ,
    (∃ x ≥ 5, x^2 < z) ⇒
      25 < z.

Proof.
Take z ∈ ℝ.
Assume that (∃ x ≥ 5, x^2 < z). 
Obtain such a x.
We conclude that (& 25 ≤ x^2 < z).
Qed.

## 12. Argue by contradiction

### Example

Lemma example_contradicition :
  ∀ x ∈ ℝ,
    (∀ ε > 0, x > 1 - ε) ⇒
      x ≥ 1.
Proof.
Take x ∈ (ℝ).
Assume that (∀ ε > 0, x > 1 - ε) (i).
We need to show that (x ≥ 1).
We argue by contradiction.
Assume that (¬ (x ≥ 1)).
It holds that ((1 - x) > 0).
By (i) it holds that (x > 1 - (1 - x)).
Contradiction.
Qed.

Lemma exercise_contradiction :
  ∀ x ∈ ℝ,
    (∀ ε > 0, x < ε)
      ⇒ x ≤ 0.
Proof.
Take x ∈ ℝ.
Assume that (∀ ε > 0, x < ε) (i).
We argue by contradiction.
Assume that (¬ (x ≤ 0)).
It holds that (x > 0).
By (i) it holds that (x < x).
Contradiction.
Qed.

## 13. Split into cases

### Example

Lemma example_cases :
  ∀ x ∈ ℝ, ∀ y ∈ ℝ,
    max(x, y) = x ∨ max(x, y) = y.
Proof.
Take x ∈ (ℝ).
Take y ∈ (ℝ).
Either (x < y) or (x ≥ y).
- Case (x < y).
  It suffices to show that (max(x, y) = y).
  We conclude that (max(x, y) = y).
- Case (x ≥ y).
  It suffices to show that (max(x, y) = x).
  We conclude that (max(x, y) = x).
Qed.

Lemma exercises_cases :
  ∀ x ∈ ℝ, ∀ y ∈ ℝ,
    min(x, y) = x ∨ min(x, y) = y.
Proof.
Take x ∈ ℝ.
Take y ∈ ℝ.
Either (x < y) or (x ≥ y).
- Case (x < y).
It suffices to show that (min(x, y) = x).
We conclude that (min(x, y) = x).
- Case (x ≥ y).
It suffices to show that (min(x, y) = y).
We conclude that (min(x, y) = y).
Qed.

## 14. Prove two statements: A ∧ B

### Example

Lemma example_both_statements :
  ∀ x ∈ ℝ, x^2 ≥ 0 ∧ | x | ≥ 0.
Proof.
Take x ∈ (ℝ).
We show both statements.
* We need to show that (x^2 ≥ 0).
  We conclude that (x^2 ≥ 0).
* We need to show that (| x | ≥ 0).
  We conclude that (| x | ≥  0).
Qed.

Lemma exercise_both_statements :
  ∀ x ∈ ℝ, 0 * x = 0 ∧ x + 1 > x.
Proof.
Take x ∈ ℝ.
We show both statements.
* We need to show that (0 * x = 0).
  We conclude that (0 * x = 0).
* We need to show that (x + 1 > x).
  We conclude that (x + 1 > x).
Qed.

## 15. Show both directions

### Example

Lemma example_both_directions :
  ∀ x ∈ ℝ, ∀ y ∈ ℝ,
    x < y ⇔ y > x.
Proof.
Take x ∈ (ℝ).
Take y ∈ (ℝ).
We show both directions.
++ We need to show that (x < y ⇒ y > x).
   Assume that (x < y).
   We conclude that (y > x).
++ We need to show that (y > x ⇒ x < y).
   Assume that (y > x).
   We conclude that (x < y).
Qed.

Lemma exercise_both_directions :
  ∀ x ∈ ℝ, x > 1 ⇔ x - 1 > 0.
Proof.
Take x ∈ ℝ.
We show both directions.
++ We need to show that (x > 1 ⇒ x - 1 > 0).
Assume that (x > 1).
We conclude that (x - 1 > 0).
++ We need to show that (x - 1 > 0 ⇒ x > 1).
Assume that (x - 1 > 0).
We conclude that (x > 1).
Qed.

## 16. Proof by induction

### Example

Lemma example_induction :
  ∀ n : ℕ → ℕ, (∀ k ∈ ℕ, (n(k) < n(k+1))%nat) ⇒
    ∀ k ∈ ℕ, (k ≤ n(k))%nat.
Proof.
Take n : (ℕ → ℕ).
Assume that (∀ k ∈ ℕ, n(k) < n(k+1))%nat.
We use induction on k.
+ We first show the base case ((0 ≤ n(0))%nat).
  We conclude that (0 ≤ n(0))%nat.
+ We now show the induction step.
  Take k ∈ ℕ.
  Assume that (k ≤ n(k))%nat.
  It holds that (n(k) < n(k+1))%nat.
  It holds that (n(k) + 1 ≤ n(k+1))%nat.
  We conclude that (& k + 1 ≤ n(k) + 1 ≤ n(k + 1))%nat.
Qed.

Lemma exercise_induction :
  ∀ F : ℕ → ℕ, (∀ k ∈ ℕ, (F(k+1) = F(k))%nat) ⇒
    ∀ k ∈ ℕ, (F(k) = F(0))%nat.
Proof.
Take F : (ℕ → ℕ).
Assume that (∀ k ∈ ℕ, (F(k+1) = F(k))%nat).
We use induction on k.
+ We first show the base case ((F(0) = F(0))%nat).
We conclude that ((F(0) = F(0))%nat).
+ We now show the induction step.
Take k ∈ ℕ.
Assume that ((F(k) = F(0))%nat).
It holds that (F(k+1) = F(k))%nat.
We conclude that (& F(k+1) = F(k) = F(0))%nat.
Qed.

## 17. Working with definitions

### Example

Definition square (x : ℝ) := x^2.

Lemma example_def :
  ∀ x ∈ ℝ, square x ≥ 0.
Proof.
Take x ∈ (ℝ).
We need to show that (x^2 ≥ 0).
We conclude that (x^2 ≥ 0).
Qed.

Lemma exercise_def :
  ∀ x ∈ ℝ, - (square x) ≤ 0.
Proof.
Take x ∈ (ℝ).
We need to show that (- x^2 ≤ 0).
It holds that (x^2 ≥ 0).
We conclude that (- x^2 ≤ 0).
Qed.
